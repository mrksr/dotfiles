# P10k instant prompt
if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
    source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
fi

### Added by zi's installer
if [[ ! -f $HOME/.zi/bin/zi.zsh ]]; then
    print -P "%F{33}▓▒░ %F{220}Installing ZI Plugin Manager (z-shell/zi)…%f"
    zi_home="${HOME}/.zi" && mkdir -p $zi_home
    git clone https://github.com/z-shell/zi.git "${zi_home}/bin"
    print -P "%F{33}▓▒░ %F{34}Installation successful.%f" || \
    print -P "%F{160}▓▒░ The clone has failed.%f"
fi
source "${HOME}/.zi/bin/zi.zsh"
autoload -Uz _zi
(( ${+_comps} )) && _comps[zi]=_zi

# Load a few important annexes, without Turbo
# (this is currently required for annexes)
zi light-mode for \
    z-shell/z-a-meta-plugins \
    z-shell/z-a-eval \
    @annexes

### End of zi's installer chunk

# NOTE(mrksr): Disable compinit unsecure folder security feature to avoid
# annoying issues with sudo.
ZI[COMPINIT_OPTS]="-C -i"

# vi-mode
zi lucid for \
    atinit"KEYTIMEOUT=1" \
    atload"
    autoload -Uz edit-command-line
    zle -N edit-command-line
    bindkey -M vicmd '^v' edit-command-line
    bindkey -M vicmd '^b' edit-command-line
    " \
        OMZP::vi-mode


# Colors and theme
zi lucid for \
    atclone"dircolors -b LS_COLORS > clrs.zsh" \
    atpull'%atclone' pick"clrs.zsh" nocompile'!' \
    atload'zstyle ":completion:*" list-colors “${(s.:.)LS_COLORS}”' \
        trapd00r/LS_COLORS \
    atload"base16_railscasts" \
        chriskempson/base16-shell

zi depth'1' light-mode for \
    atload'source ~/.p10k.zsh' \
        romkatv/powerlevel10k


# oh-my-zshell config
zi wait lucid for \
    OMZL::functions.zsh \
    OMZL::clipboard.zsh \
    OMZL::completion.zsh \
    OMZL::correction.zsh \
    atinit"setopt auto_cd" \
        OMZL::directories.zsh \
    atinit"
        HISTSIZE=1000000
        SAVEHIST=1000000
    " \
    OMZL::history.zsh \
    OMZL::misc.zsh \
    OMZL::spectrum.zsh \
    OMZL::termsupport.zsh


# Plugins
zi wait lucid for \
    OMZP::colored-man-pages \
    OMZP::extract \
    OMZP::git \
    OMZP::gitignore \
    OMZP::mosh \
    OMZP::screen


# Install binaries
zi wait lucid light-mode for \
    @zsh-users+fast \
    @z-shell \
    @py-utils \
    skip'exa' @console-tools

zi wait lucid light-mode as'command' from'gh-r' sbin for \
    jesseduffield/lazydocker \
    jesseduffield/lazygit \
    eza-community/eza

zi wait pack"bgn-binary+keys" for fzf

# NOTE(mrksr): The atload is a hack to set-fast theme _after_ the default theme
# in the meta-plugin.
zi wait lucid for \
    atload"
        fast-theme -q clean
    " \
    atinit"
        zstyle ':completion:complete:*:options' sort false
        zstyle ':completion:*:git-checkout:*' sort false
        zstyle ':completion:*:descriptions' format '[%d]'
        zstyle ':completion:*' list-colors \${(s.:.)LS_COLORS}
        zstyle ':fzf-tab:complete:cd:*' fzf-preview 'eza -1 --color=always \$realpath'
    " \
    atload"
        # Use fzf-tab after second tab press
        fzf-tab-partial-and-complete() {
            if [[ \$LASTWIDGET = 'fzf-tab-partial-and-complete' ]]; then
                fzf-tab-complete
            else
                zle complete-word
            fi
        }

        zle -N fzf-tab-partial-and-complete
        bindkey '^I' fzf-tab-partial-and-complete
    " \
    Aloxaf/fzf-tab \
    MichaelAquilina/zsh-autoswitch-virtualenv


# Own config
setopt extendedglob
setopt null_glob

if command -v eza > /dev/null; then
    alias ls="eza --icons --group-directories-first"
    alias ll="ls --long --group --header --git"
    alias l="ll --all"

    alias tree="eza --long --tree"
else
    alias ls="ls --color=tty --group-directories-first -h"
fi
alias lsd="ls -lhd */"

if [[ $(uname) != "Darwin" ]]; then
    alias open="open-background"
    open-background() {
        xdg-open "$@" &>/dev/null &!
    }
fi

alias texdoco="online-texdoc"
online-texdoc() {
    DOC_FILE="${TMPDIR:-/tmp}/online_texdoc.${1}.pdf"
    if [[ ! -f "$DOC_FILE" ]]; then
        wget http://texdoc.net/pkg/${1} -O "$DOC_FILE"
    fi
    open-background "$DOC_FILE"
}

if command -v bat > /dev/null; then
    alias cat="bat"
fi

if command -v nvim > /dev/null; then
    alias vim="nvim"
    export MANPAGER="nvim +Man!"
fi

if command -v aria2c > /dev/null; then
    alias youtube-dl="youtube-dl \
        --external-downloader aria2c \
        --external-downloader-args '-c -j 5 -x 5 -s 5 -k 2M' \
        "
fi
